TYPE "User_data_type_1"
VERSION : 0.1
   STRUCT
      twobyte : Array[0..1] of Byte;
   END_STRUCT;

END_TYPE

FUNCTION "FC Move Variant" : Void
TITLE = FC Move Variant
{ S7_Optimized_Access := 'FALSE' }
AUTHOR : Widelund
VERSION : 1.0
   VAR_INPUT 
      "i-Data" : Variant;
   END_VAR

   VAR_OUTPUT 
      "o-Data" : Variant;
      "o-Count" : UDInt;
   END_VAR

   VAR_TEMP 
      "t-Data" : Array[0..2048] of Byte;
      "t-Int" : Int;
      "t-Pos" : DInt;
   END_VAR


BEGIN
	//Kopiera Variant IN till Variant UT
	#"t-Pos" := 0;
	#"t-Int" := Serialize(SRC_VARIABLE := #"i-Data", DEST_ARRAY => #"t-Data", POS := #"t-Pos");
	//Utvärdera antal element i arrayen
	IF (IS_ARRAY(#"i-Data")) THEN
	    #"o-Count" := CountOfElements(#"i-Data");
	END_IF;
	#"t-Pos" := 0;
	#"t-Int" := Deserialize(SRC_ARRAY := #"t-Data", DEST_VARIABLE => #"o-Data", POS := #"t-Pos");
END_FUNCTION

FUNCTION_BLOCK "SWAP_STRUCT"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      "i-swap" : Bool;
   END_VAR

   VAR_OUTPUT 
      "o-countOfElements0" : UDInt;
      "o-countOfElements1" : UDInt;
   END_VAR

   VAR_IN_OUT 
      "io-struct0" : Variant;
      "io-struct1" : Variant;
   END_VAR

   VAR 
      "s-R_TRIG" {InstructionName := 'R_TRIG'; LibVersion := '1.0'} : R_TRIG;
   END_VAR

   VAR_TEMP 
      "t-struct" : Array[0..2] of "User_data_type_1";
      "t-countOfElements" : Array[0..1] of UDInt;
   END_VAR


BEGIN
	#"s-R_TRIG"(CLK := #"i-swap");
	
	IF #"s-R_TRIG".Q THEN
	    
	    REGION read 
	    "FC Move Variant"("i-Data" := #"io-struct0",
	                      "o-Data" => #"t-struct"[0],
	                      "o-Count" => #"t-countOfElements"[0]);
	    
	    "FC Move Variant"("i-Data" := #"io-struct1",
	                      "o-Data" => #"t-struct"[1],
	                      "o-Count" => #"t-countOfElements"[1]);
	    END_REGION
	
	    REGION swap
	    //swap struct0 and struct1
	    //struct2 is a temp placeholder
	    #"t-struct"[2] := #"t-struct"[0];
	    #"t-struct"[0] := #"t-struct"[1];
	    #"t-struct"[1] := #"t-struct"[2];
	    END_REGION
	
	    REGION write
	    "FC Move Variant"("i-Data" := #"t-struct"[0],
	                      "o-Data" => #"io-struct0",
	                      "o-Count" => #"t-countOfElements"[0]);
	    
	    "FC Move Variant"("i-Data" := #"t-struct"[1],
	                      "o-Data" => #"io-struct1",
	                      "o-Count" => #"t-countOfElements"[1]);
	    END_REGION
	
	    REGION countOfElements
	        #"o-countOfElements0" := #"t-countOfElements"[0];
	        #"o-countOfElements1" := #"t-countOfElements"[1];
	    END_REGION ;
	    
	END_IF;
END_FUNCTION_BLOCK

